grammar org.eclipse.viatra2.patternlanguage.core.PatternLanguage with org.eclipse.xtext.xbase.Xbase

generate patternLanguage "http://www.eclipse.org/viatra2/patternlanguage/core/PatternLanguage"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.ecore" as xbase

PatternModel:
	imports+=Import*
	patterns+=Pattern*;

Import:
	'import' ePackage=[ecore::EPackage|STRING]// ('as' alias=ID)?
	//'import' importURI=STRING
;
	
Pattern:
	annotations+=Annotation*
	modifiers+=Modifiers*
	'pattern'
	name=ID
	'('
	(parameters+=Parameter (',' parameters+=Parameter)*)?
	')'
	'='
	bodies+=PatternBody	('or' bodies+=PatternBody)*
;

Annotation:
	'@'
	name=ID
;

Modifiers:
	shareable?='shareable' 
;

Variable:
	Parameter | LocalVariable
;

Parameter: 
	name=ID
	(':' type=Type)?
;

LocalVariable returns Variable:
	name=ID
;

VariableReference:
	var = [Variable]
;

Type returns EMFType:
	{ClassType}
	typename=[ecore::EClass]
;

RefType returns EMFType:
	{ReferenceType}
	typename=[ecore::EReference]
;


PatternBody returns PatternBody:
{PatternBody}
    (name=ID)?
	'{'
	  (constraints += Constraint ';')+
	'}'
;

Constraint:
	EClassConstraint | CheckConstraint | PatternCompositionConstraint | ExpressionConstraint
;

EClassConstraint returns Variable:
	{EClassConstraint}
	type = Type '(' name=ID ')'
;


PatternCompositionConstraint returns Constraint:
	{PatternCompositionConstraint}
	negative?='neg'?
	'find'
	patternRef = [Pattern]
	'('
	  (parameters+=VariableReference (',' parameters+=VariableReference)*)?
	')'
;

CheckConstraint returns Constraint:
	{CheckConstraint}
	'check' '(' expression = XExpression ')'
;

ExpressionConstraint returns Constraint:
	{ExpressionConstraint}
	negative?='neg'?
	head=ExpressionHead
;

ExpressionHead:
	type = Type '.' tail = ExpressionTail '('src=VariableReference ',' dst=VariableReference ')'
;

ExpressionTail:
	type = RefType ('[' index=INT ']')? closure?='*'? ('.' tail = ExpressionTail)?
;

